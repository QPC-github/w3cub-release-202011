
<!DOCTYPE HTML>

<html lang="en" class="_theme-default">

<head>
  <meta charset="utf-8">
  <title>10. Appendix B&#58; Activity Access Callback Interface - Erlang 21 - W3cubDocs</title>
  
  <meta name="description" content=" © 2010–2017 Ericsson ABLicensed under the Apache License, Version 2.0. ">
  <meta name="keywords" content="appendix, b, activity, access, callback, interface, erlang, erlang~21">
  <meta name="HandheldFriendly" content="True">
  <meta name="MobileOptimized" content="320">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <meta name="mobile-web-app-capable" content="yes">
  
  <link rel="canonical" href="https://docs.w3cub.com/erlang~21/lib/mnesia-4.15.4/doc/html/mnesia_app_b.html">
  <link href="/favicon.png" rel="icon">
  <link rel="stylesheet" type="text/css" href="/assets/application-e4ebd3a2a5652ff55173659804c4390a004917f3bdd17b5bb3ba78ea5c9c46fe181cadaac34517ccd815f5bdc982bbfe67179d6f4ac2f084ef2265e2a3dc8dc5.css" integrity="sha512-5OvToqVlL/VRc2WYBMQ5CgBJF/O90Xtbs7p46lycRv4YHK2qw0UXzNgV9b3Jgrv+Zxedb0rC8ITvImXio9yNxQ==" crossorigin="anonymous">
  <script type="text/javascript" integrity="sha512-EpkDeu98lN/jPKijllzVWdRg/dUSSMCaldYZNFz6bcNoBvpWRNz0HSTRQJ3ENmQc5Cuj1zDW1vHd7b0DzpOgyA==" crossorigin="anonymous" src="/assets/application-1299037aef7c94dfe33ca8a3965cd559d460fdd51248c09a95d619345cfa6dc36806fa5644dcf41d24d1409dc436641ce42ba3d730d6d6f1ddedbd03ce93a0c8.js"></script>
  <script src="/json/erlang~21.js"></script>
  
  <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-71174418-1']);
    _gaq.push(['_trackPageview']);

    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
  <script data-ad-client="ca-pub-2572770204602497" async src="https://pagead2.googlesyndication.com/pagead/js/adsbygoogle.js"></script>
  <script async custom-element="amp-auto-ads"
  src="https://cdn.ampproject.org/v0/amp-auto-ads-0.1.js">
</script>


</head>

<body>
	<amp-auto-ads type="adsense"
              data-ad-client="ca-pub-2572770204602497">
	</amp-auto-ads>
	<div class="_app">
	<header class="_header">
  
  <form class="_search">
    <input type="search" class="_search-input" placeholder="Search&hellip;" autocomplete="off" autocapitalize="off" autocorrect="off" spellcheck="false" maxlength="20">
    <a class="_search-clear"></a>
    <div class="_search-tag"></div>
  </form>
  
  <a class="_home-link" href="/" ></a>
  <a class="_menu-link"></a>
  <h1 class="_logo">
    <a href="/" class="_nav-link" title="API Documentation Browser">W3cubDocs</a>
  </h1>
  
  <span class="_logo-sub-nav">/</span><span class="_logo-sub-nav"><a href="/erlang~21/" class="_nav-link" title="" style="margin-left:0;">Erlang 21</a></span>
  
  <nav class="_nav">
    <a href="https://tools.w3cub.com/?_sp=docs" target="_blank" class="_nav-link ">W3cubTools</a>
    <a href="/cheatsheets/" class="_nav-link ">Cheatsheets</a>
    <a href="/about/" class="_nav-link ">About</a>
  </nav>
</header>
	<section class="_sidebar">
		<div class="_list-wrap">
			<div class="_list">
			
			</div>
		</div>
	</section>
	<section class="_container ">
		<div class="_content">
			<ins class="adsbygoogle"
     style="display:block"
     data-ad-client="ca-pub-2572770204602497"
     data-ad-slot="6861657091"
     data-ad-format="link"
     data-full-width-responsive="true"></ins>
<script>
     (adsbygoogle = window.adsbygoogle || []).push({});
</script>
			<div class="_page _erlang">
				
				
<h1>10 Appendix B: Activity Access Callback Interface</h1> <h2><span onmouseover="document.getElementById('ghlink-mnesia_access-callback-behavior-id87480').style.visibility = 'visible';" onmouseout="document.getElementById('ghlink-mnesia_access-callback-behavior-id87480').style.visibility = 'hidden';" id="mnesia_access-callback-behavior">10.1 mnesia_access Callback Behavior</span></h2>  <pre data-language="erlang">-module(mnesia_frag).

%% Callback functions when accessed within an activity
-export([
	 lock/4,
	 write/5, delete/5, delete_object/5,
	 read/5, match_object/5, all_keys/4,
	 select/5,select/6,select_cont/3,
	 index_match_object/6, index_read/6,
	 foldl/6, foldr/6, table_info/4,
	 first/3, next/4, prev/4, last/3,
	 clear_table/4
       ]).</pre>  <pre data-language="erlang">%% Callback functions which provides transparent
%% access of fragmented tables from any activity
%% access context.

lock(ActivityId, Opaque, {table , Tab}, LockKind) -&gt;
    case frag_names(Tab) of
	[Tab] -&gt;
	    mnesia:lock(ActivityId, Opaque, {table, Tab}, LockKind);
	Frags -&gt;
	    DeepNs = [mnesia:lock(ActivityId, Opaque, {table, F}, LockKind) ||
			 F &lt;- Frags],
	    mnesia_lib:uniq(lists:append(DeepNs))
    end;

lock(ActivityId, Opaque, LockItem, LockKind) -&gt;
    mnesia:lock(ActivityId, Opaque, LockItem, LockKind).

write(ActivityId, Opaque, Tab, Rec, LockKind) -&gt;
    Frag = record_to_frag_name(Tab, Rec),
    mnesia:write(ActivityId, Opaque, Frag, Rec, LockKind).

delete(ActivityId, Opaque, Tab, Key, LockKind) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    mnesia:delete(ActivityId, Opaque, Frag, Key, LockKind).

delete_object(ActivityId, Opaque, Tab, Rec, LockKind) -&gt;
    Frag = record_to_frag_name(Tab, Rec),
    mnesia:delete_object(ActivityId, Opaque, Frag, Rec, LockKind).

read(ActivityId, Opaque, Tab, Key, LockKind) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    mnesia:read(ActivityId, Opaque, Frag, Key, LockKind).

match_object(ActivityId, Opaque, Tab, HeadPat, LockKind) -&gt;
    MatchSpec = [{HeadPat, [], ['$_']}],
    select(ActivityId, Opaque, Tab, MatchSpec, LockKind).

select(ActivityId, Opaque, Tab, MatchSpec, LockKind) -&gt;
    do_select(ActivityId, Opaque, Tab, MatchSpec, LockKind).


select(ActivityId, Opaque, Tab, MatchSpec, Limit, LockKind) -&gt;
    init_select(ActivityId, Opaque, Tab, MatchSpec, Limit, LockKind).


all_keys(ActivityId, Opaque, Tab, LockKind) -&gt;
    Match = [mnesia:all_keys(ActivityId, Opaque, Frag, LockKind)
	     || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

clear_table(ActivityId, Opaque, Tab, Obj) -&gt;
    [mnesia:clear_table(ActivityId, Opaque, Frag, Obj)  || Frag &lt;- frag_names(Tab)],
    ok.

index_match_object(ActivityId, Opaque, Tab, Pat, Attr, LockKind) -&gt;
    Match =
	[mnesia:index_match_object(ActivityId, Opaque, Frag, Pat, Attr, LockKind)
	 || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

index_read(ActivityId, Opaque, Tab, Key, Attr, LockKind) -&gt;
    Match =
	[mnesia:index_read(ActivityId, Opaque, Frag, Key, Attr, LockKind)
	     || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

foldl(ActivityId, Opaque, Fun, Acc, Tab, LockKind) -&gt;
    Fun2 = fun(Frag, A) -&gt;
		   mnesia:foldl(ActivityId, Opaque, Fun, A, Frag, LockKind)
	   end,
    lists:foldl(Fun2, Acc, frag_names(Tab)).

foldr(ActivityId, Opaque, Fun, Acc, Tab, LockKind) -&gt;
    Fun2 = fun(Frag, A) -&gt;
		   mnesia:foldr(ActivityId, Opaque, Fun, A, Frag, LockKind)
	   end,
    lists:foldr(Fun2, Acc, frag_names(Tab)).

table_info(ActivityId, Opaque, {Tab, Key}, Item) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    table_info2(ActivityId, Opaque, Tab, Frag, Item);
table_info(ActivityId, Opaque, Tab, Item) -&gt;
    table_info2(ActivityId, Opaque, Tab, Tab, Item).

table_info2(ActivityId, Opaque, Tab, Frag, Item) -&gt;
    case Item of
	size -&gt;
	    SumFun = fun({_, Size}, Acc) -&gt; Acc + Size end,
	    lists:foldl(SumFun, 0, frag_size(ActivityId, Opaque, Tab));
	memory -&gt;
	    SumFun = fun({_, Size}, Acc) -&gt; Acc + Size end,
	    lists:foldl(SumFun, 0, frag_memory(ActivityId, Opaque, Tab));
	base_table -&gt;
	    lookup_prop(Tab, base_table);
	node_pool -&gt;
	    lookup_prop(Tab, node_pool);
	n_fragments -&gt;
	    FH = lookup_frag_hash(Tab),
	    FH#frag_state.n_fragments;
	foreign_key -&gt;
	    FH = lookup_frag_hash(Tab),
	    FH#frag_state.foreign_key;
	foreigners -&gt;
	    lookup_foreigners(Tab);
	n_ram_copies -&gt;
	    length(val({Tab, ram_copies}));
	n_disc_copies -&gt;
	    length(val({Tab, disc_copies}));
	n_disc_only_copies -&gt;
	    length(val({Tab, disc_only_copies}));
	n_external_copies -&gt;
	    length(val({Tab, external_copies}));

	frag_names -&gt;
	    frag_names(Tab);
	frag_dist -&gt;
	    frag_dist(Tab);
	frag_size -&gt;
	    frag_size(ActivityId, Opaque, Tab);
	frag_memory -&gt;
	    frag_memory(ActivityId, Opaque, Tab);
	_ -&gt;
	    mnesia:table_info(ActivityId, Opaque, Frag, Item)
    end.

first(ActivityId, Opaque, Tab) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:first(ActivityId, Opaque, Tab);
	FH -&gt;
	    FirstFrag = Tab,
	    case mnesia:first(ActivityId, Opaque, FirstFrag) of
		'$end_of_table' -&gt;
		    search_first(ActivityId, Opaque, Tab, 1, FH);
		Next -&gt;
		    Next
	    end
    end.

search_first(ActivityId, Opaque, Tab, N, FH) when N &lt; FH#frag_state.n_fragments -&gt;
    NextN = N + 1,
    NextFrag = n_to_frag_name(Tab, NextN),
    case mnesia:first(ActivityId, Opaque, NextFrag) of
	'$end_of_table' -&gt;
	    search_first(ActivityId, Opaque, Tab, NextN, FH);
	Next -&gt;
	    Next
    end;
search_first(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.

last(ActivityId, Opaque, Tab) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:last(ActivityId, Opaque, Tab);
	FH -&gt;
	    LastN = FH#frag_state.n_fragments,
	    search_last(ActivityId, Opaque, Tab, LastN, FH)
    end.

search_last(ActivityId, Opaque, Tab, N, FH) when N &gt;= 1 -&gt;
    Frag = n_to_frag_name(Tab, N),
    case mnesia:last(ActivityId, Opaque, Frag) of
	'$end_of_table' -&gt;
	    PrevN = N - 1,
	    search_last(ActivityId, Opaque, Tab, PrevN, FH);
	Prev -&gt;
	    Prev
    end;
search_last(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.

prev(ActivityId, Opaque, Tab, Key) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:prev(ActivityId, Opaque, Tab, Key);
	FH -&gt;
	    N = key_to_n(FH, Key),
	    Frag = n_to_frag_name(Tab, N),
	    case mnesia:prev(ActivityId, Opaque, Frag, Key) of
		'$end_of_table' -&gt;
		    search_prev(ActivityId, Opaque, Tab, N);
		Prev -&gt;
		    Prev
	    end
    end.

search_prev(ActivityId, Opaque, Tab, N) when N &gt; 1 -&gt;
    PrevN = N - 1,
    PrevFrag = n_to_frag_name(Tab, PrevN),
    case mnesia:last(ActivityId, Opaque, PrevFrag) of
	'$end_of_table' -&gt;
	    search_prev(ActivityId, Opaque, Tab, PrevN);
	Prev -&gt;
	    Prev
    end;
search_prev(_ActivityId, _Opaque, _Tab, _N) -&gt;
    '$end_of_table'.

next(ActivityId, Opaque, Tab, Key) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:next(ActivityId, Opaque, Tab, Key);
	FH -&gt;
	    N = key_to_n(FH, Key),
	    Frag = n_to_frag_name(Tab, N),
	    case mnesia:next(ActivityId, Opaque, Frag, Key) of
		'$end_of_table' -&gt;
		    search_next(ActivityId, Opaque, Tab, N, FH);
		Prev -&gt;
		    Prev
	    end
    end.

search_next(ActivityId, Opaque, Tab, N, FH) when N &lt; FH#frag_state.n_fragments -&gt;
    NextN = N + 1,
    NextFrag = n_to_frag_name(Tab, NextN),
    case mnesia:first(ActivityId, Opaque, NextFrag) of
	'$end_of_table' -&gt;
	    search_next(ActivityId, Opaque, Tab, NextN, FH);
	Next -&gt;
	    Next
    end;
search_next(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.</pre>
<div class="_attribution">
  <p class="_attribution-p">
    © 2010–2017 Ericsson AB<br>Licensed under the Apache License, Version 2.0.<br>
    
  </p>
</div>

				
			</div>
			<ins class="adsbygoogle"
     style="display:block"
     data-ad-client="ca-pub-2572770204602497"
     data-ad-slot="1992473792"
     data-ad-format="auto"
     data-full-width-responsive="true"></ins>
<script>
     (adsbygoogle = window.adsbygoogle || []).push({});
</script>
		</div>
	</section>

	</div>
	<svg style="display:none">
		<symbol id="icon-dir"><svg viewBox="0 0 20 20"><path d="M15 10c0 .3-.305.515-.305.515l-8.56 5.303c-.625.41-1.135.106-1.135-.67V4.853c0-.777.51-1.078 1.135-.67l8.56 5.305S15 9.702 15 10z"/></svg></symbol>
	  </svg>
</body>
</html>
